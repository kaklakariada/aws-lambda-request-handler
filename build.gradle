plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.hierynomus.license' version '0.14.0'
}

repositories {
    jcenter()
}

version = '0.4.0'
group = 'com.github.kaklakariada'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    jacksonVersion = '2.9.6'
    log4jVersion = '2.11.0'
    junitPlatformVersion = '1.2.0'
    junitJupiterVersion = '5.2.0'
    mockitoVersion = '2.19.0'
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

dependencies {
    api 'org.slf4j:slf4j-api:1.7.25'
    api "javax.inject:javax.inject:1"
    api 'com.amazonaws:aws-lambda-java-core:1.2.0'
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testCompile "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"

    testRuntime "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testRuntime "org.apache.logging.log4j:log4j-jul:${log4jVersion}"
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                asNode().appendNode('licenses').appendNode('license').
                         appendNode('name', 'GPLv3').parent().
                         appendNode('url', 'https://www.gnu.org/licenses/gpl-3.0.html').parent().
                         appendNode('distribution', 'repo')
            }
        }
    }
}

bintray {
    def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	user = bintrayUser
	key = bintrayApiKey
	publications = ['BintrayPublication']
	pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kaklakariada/aws-lambda-request-handler.git'
        version {
            name = project.version
        }
    }
}

license {
    header = file('gradle/license-header.txt')
}
