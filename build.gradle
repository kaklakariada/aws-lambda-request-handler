
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M5'
	}
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.hierynomus.license' version '0.14.0'
}

apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
}

version = '0.2.0'
group = 'com.github.kaklakariada'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    jacksonVersion = '2.8.9'
    junit4Version = '4.12'
    junitPlatformVersion = '1.0.0-M5'
    junitJupiterVersion = '5.0.0-M5'
    log4jVersion = '2.8.2'
}

compileTestJava {
    options.compilerArgs += '-parameters'
}

dependencies {
    compile 'com.amazonaws:aws-lambda-java-core:1.1.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    
    testCompile 'org.mockito:mockito-core:2.8.47'
    
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    
    testCompile "junit:junit:${junit4Version}"
    testCompile "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"

    testRuntime "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testRuntime "org.apache.logging.log4j:log4j-jul:${log4jVersion}"
}


junitPlatform {
    enableStandardTestTask true
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                asNode().appendNode('licenses').appendNode('license').
                         appendNode('name', 'GPLv3').parent().
                         appendNode('url', 'https://www.gnu.org/licenses/gpl-3.0.html').parent().
                         appendNode('distribution', 'repo')
            }
        }
    }
}

bintray {
    def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    def bintrayApiKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	user = bintrayUser
	key = bintrayApiKey
	publications = ['BintrayPublication']
	pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/kaklakariada/aws-lambda-request-handler.git'
        version {
            name = project.version
        }
    }
}

license {
    header = file('gradle/license-header.txt')
    ext.year = 2017
    ext.name = 'Christoph Pirkl'
    ext.email = 'christoph at users.sourceforge.net'
}
